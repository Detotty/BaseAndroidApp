apply plugin: 'com.android.application'
apply plugin: 'checkstyle'

android {

    packagingOptions {
        exclude 'LICENSE.txt'
    }

    lintOptions {
        abortOnError false
        disable 'ContentDescription'
        disable 'InvalidPackage'
    }

    compileSdkVersion 21
    buildToolsVersion "21.1.0"

    defaultConfig {
        applicationId "packagename.app.com.appname"
        minSdkVersion 15
        targetSdkVersion 21
        versionCode System.getenv("BUILD_NUMBER") as Integer ?: 0
        versionName System.getenv("BUILD_NUMBER") ?: "IDE"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            applicationIdSuffix ".debug"
            testCoverageEnabled true
        }
    }
}

task checkstyle(type: Checkstyle) {
    source 'src/'
    include '**/*.java'
    exclude '**/gen/**'
    classpath = files()
    reports {
        xml {
            destination "build/outputs/reports/checkstyle-results.xml"
        }
    }
}

project.gradle.taskGraph.whenReady {
    connectedAndroidTest {
        ignoreFailures = true
    }
}

task checkstyleReport(dependsOn: 'checkstyle') << {
    if (file("build/outputs/reports/checkstyle-results.xml").exists()) {
        ant.xslt(in: "build/outputs/reports/checkstyle-results.xml",
                style: "config/checkstyle/checkstyle.xsl",
                out: "build/outputs/reports/checkstyle-results.html"
        )
    }
}

dependencies {
    provided 'com.squareup.dagger:dagger-compiler:1.2.2'
    compile 'com.android.support:appcompat-v7:21.0.2'
    compile 'net.hockeyapp.android:HockeySDK:3.5.0-b.4'
    compile 'com.squareup.okhttp:okhttp:2.1.0'
    compile 'com.squareup.okhttp:okhttp-urlconnection:2.1.0'
    compile 'com.squareup.retrofit:retrofit:1.8.0'
    compile 'de.greenrobot:eventbus:2.4.0'
    compile 'com.squareup.picasso:picasso:2.4.0'
    compile 'com.squareup.dagger:dagger:1.2.2'
    compile 'com.jakewharton:butterknife:6.0.0'
    androidTestCompile 'org.mockito:mockito-core:1.9.5'
    androidTestCompile 'com.google.dexmaker:dexmaker:1.0'
    androidTestCompile 'com.google.dexmaker:dexmaker-mockito:1.1'
    androidTestCompile 'org.hamcrest:hamcrest-core:1.3'
    androidTestCompile 'org.hamcrest:hamcrest-library:1.3'
}
